// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./ERC721A.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Strings.sol"; 
import "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import { IERC2981, IERC165 } from "@openzeppelin/contracts/interfaces/IERC2981.sol";

contract Contract is ERC721A, IERC2981, Ownable, ReentrancyGuard {
    using Strings for uint256;
    mapping(address => uint256) public whitelistClaimed;
    mapping(address => uint256) public publicClaimed;

    string public hiddenMetadataUri;
    string public baseURI;
    string public baseExtension = ".json";
    bool public whitelistEnabled = true;
    bool public paused = true;
    bool public revealed;
    bytes32 public merkleRoot;
    uint256 public maxWhitelist = 2;
    uint256 public maxPublic = 2;
    uint256 public mintPerTx = 2;
    uint256 public maxSupply = 10000;
    uint256 public presaleCost = .1 ether;
    uint256 public publicCost = .1 ether;

    uint96 public royaltyFeesInBips;
    address public royaltyAddress;
    string private contractURI;

    constructor(
        string memory _initBaseURI,
        uint96 _royaltyFeesInBips,
        string memory _initialContractURI
    ) ERC721A("Test Contract", "TC") {
        setBaseURI(_initBaseURI);
        royaltyFeesInBips = _royaltyFeesInBips;
        royaltyAddress = owner();
        contractURI = _initialContractURI;
        _safeMint(msg.sender, 2);
    }

    // whitelist mint
    function whitelistMint(uint256 quantity, bytes32[] calldata _merkleProof)
        public
        payable
    {
        uint256 supply = totalSupply();
        require(!paused, "The contract is paused!");
        require(quantity > 0, "Quantity Must Be Higher Than Zero");
        require(supply + quantity <= maxSupply, "Max Supply Reached");
        require(whitelistEnabled, "The whitelist sale is not enabled!");        
        require(
            whitelistClaimed[msg.sender] + quantity <= maxWhitelist,
            "You're not allowed to mint this Much!"
        );
        require(
            quantity <= maxWhitelist,
            "You're Not Allowed To Mint more than maxMint Amount"
        );
        require(msg.value >= presaleCost * quantity, "Not enough ether!");
        bytes32 leaf = keccak256(abi.encodePacked(msg.sender));
        require(
            MerkleProof.verify(_merkleProof, merkleRoot, leaf),
            "Invalid proof!"
        );

        whitelistClaimed[msg.sender] += quantity;
        _safeMint(msg.sender, quantity);
    }

    // public mint
    function mint(uint256 quantity) external payable {
        uint256 supply = totalSupply();
        require(!paused, "The contract is paused!");
        require(!whitelistEnabled, "Public mint is disable!");
        require(quantity > 0, "Quantity Must Be Higher Than Zero!");
        require(supply + quantity <= maxSupply, "Max Supply Reached!");

        if (msg.sender != owner()) {
            require(
            publicClaimed[msg.sender] + quantity <= maxPublic,
                "You're not allowed to mint this Much!"
            );
            require(
                quantity <= maxPublic,
                "You're Not Allowed To Mint more than maxMint Amount"
            );
            require(msg.value >= publicCost * quantity, "Not enough ether!");
        }
        publicClaimed[msg.sender] += quantity;
        _safeMint(msg.sender, quantity);
    }

    function devMint(uint256 quantity) external onlyOwner {
      require(totalSupply() + quantity <= maxSupply, "reached max supply"); 
      _safeMint(msg.sender, quantity);
    }

    // internal
    function _baseURI() internal view virtual override returns (string memory) {
        return baseURI;
    }

    function tokenURI(uint256 tokenId)
        public
        view
        virtual
        override
        returns (string memory)
    {
        require(
            _exists(tokenId),
            "ERC721Metadata: URI query for nonexistent token"
        );

        if (!revealed) {
            return hiddenMetadataUri;
        }

        string memory currentBaseURI = _baseURI();

        return
            bytes(currentBaseURI).length > 0
                ? string(
                    abi.encodePacked(
                        currentBaseURI,
                        tokenId.toString(),
                        baseExtension
                    )
                )
                : "";
    }
    

    function setMax(uint256 _whitelist, uint256 _public) public onlyOwner {
        maxWhitelist = _whitelist;
        maxPublic = _public;
    }

    function setMintPerTx(uint256 quantity) public onlyOwner {
        mintPerTx = quantity;
    }

    function setMaxSupply(uint256 _amount) public onlyOwner {
        maxSupply = _amount;
    }

    function setMerkleRoot(bytes32 _merkleRoot) public onlyOwner {
        merkleRoot = _merkleRoot;
    }

    function setWhitelistEnabled(bool _state) public onlyOwner {
        whitelistEnabled = _state;
    }

    function flipSaleState() public onlyOwner {
        paused = !paused;
    }

    function setReveal(bool _state) public onlyOwner {
        revealed = _state;
    }

    function setPrice(uint256 _whitelistCost, uint256 _publicCost) public onlyOwner {
        presaleCost = _whitelistCost;
        publicCost = _publicCost;
    }

    function setHiddenMetadataUri(string memory _hiddenMetadataUri) public onlyOwner {
        hiddenMetadataUri = _hiddenMetadataUri;
    }

    function setBaseURI(string memory _newBaseURI) public onlyOwner {
        baseURI = _newBaseURI;
    }

    function setBaseExtension(string memory _newBaseExtension)
        public
        onlyOwner
    {
        baseExtension = _newBaseExtension;
    }

    function setRoyaltyReciever(address _address) public onlyOwner {
        royaltyAddress = _address;
    }

    function royaltyInfo(uint256 _tokenId, uint256 _salePrice)
        external
        view
        virtual
        override
        returns (address, uint256)
    {
        _tokenId; // silence solc warning
        return (royaltyAddress, calculateRoyalty(_salePrice));
    }

    function calculateRoyalty(uint256 _salePrice) view public returns (uint256) {
        return (_salePrice / 10000) * royaltyFeesInBips;
    }

    function supportsInterface(bytes4 interfaceId)
            public
            view
            override(ERC721A, IERC165)
            returns (bool)
    {
        return interfaceId == type(IERC2981).interfaceId || super.supportsInterface(interfaceId);
    }

    function withdraw() external onlyOwner nonReentrant {
        (bool ts, ) = payable(owner()).call{value: address(this).balance}("");
        require(ts);
    }
}